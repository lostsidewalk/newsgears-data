
plugins {
    id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'signing'

group = 'com.lostsidewalk.newsgears'
version = '0.5.11'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
    }
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'com.lostsidewalk.newsgears:newsgears-model:0.5.4'
    api 'com.lostsidewalk.newsgears:newsgears-model:0.5.4:javadoc'
    api 'com.lostsidewalk.newsgears:newsgears-model:0.5.4:sources'

    // SLF4J
    implementation 'org.slf4j:slf4j-api:2.0.5'

    // spring-boot-starter-data-jpa
    api 'org.springframework.boot:spring-boot-starter-data-jpa:3.1.2'
    // spring-boot-starter-data-redis
    api 'org.springframework.boot:spring-boot-starter-data-redis:3.1.2'
    // commons-pool2
    api 'org.apache.commons:commons-pool2:2.11.1'
    // jedis
    api 'redis.clients:jedis:4.3.1'
    // ROAM RSS/Atom
    api 'com.rometools:rome:2.1.0'
    // json
    api 'org.json:json:20230227'

    // gson
    implementation 'com.google.code.gson:gson:2.10.1'
    // commons-lang
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    // commons-collections4
    implementation 'org.apache.commons:commons-collections4:4.4'
    // postgresql driver
    implementation 'org.postgresql:postgresql:42.5.4'
    // guava
    implementation 'com.google.guava:guava:31.1-jre'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
    implementation 'com.github.therapi:therapi-runtime-javadoc:0.15.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'newsgears-data'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'newsgears-data'
                description = 'This module contains the Java classes used to store and retrieve FeedGears RSS model data to/from a JDBC data source.'
                url = 'https://www.lostsidewalk.com/dev/newsgears-data'
                licenses {
                    license {
                        name = 'GNU General Public License'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'elderscrollsjesus'
                        name = 'Michael Harris'
                        email = 'meh@lostsidewalk.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/lostsidewalk/newsgears-data.git'
                    developerConnection = 'scm:git:https://github.com/lostsidewalk/newsgears-data.git'
                    url = 'https://www.lostsidewalk.com/dev/newsgears-data'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
